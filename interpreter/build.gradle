import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'maven-publish'
}

group = 'org.pyjinn'
version = '0.13'

processResources {
    def expandProps = [
        pyjinnVersion: version,
        javaVersion: System.getProperty('java.version')
    ]
    filesMatching('build.properties') {
        expand expandProps
    }
    inputs.properties(expandProps)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':parser')
    implementation libs.gson

    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

shadowJar {
    archiveBaseName = "pyjinn-lib"
    exclude 'com/google/gson/**'
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    relocate 'org.antlr', 'org.pyjinn.shadow.antlr'
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = 'pyjinn-lib'
        }
    }
    repositories {
        mavenLocal()
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    archiveBaseName = "pyjinn-interpreter"
    archiveVersion = project.version
}